plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'ru.Fronzter'
version = '1.0'

repositories {
    mavenCentral()
    maven { name = "papermc-repo"; url = "https://repo.papermc.io/repository/maven-public/" }
    flatDir { dirs 'libs' }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly name: 'spigot-1.16.5'

    implementation name: 'packetevents-spigot-2.9.4'
    implementation 'org.apache.commons:commons-math3:3.6.1'

    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') { expand props }
}

tasks.named('shadowJar') {
    archiveClassifier.set('')
    minimize()
    from('LICENSE') { into('/') }
    from('NOTICE') { into('/') }

    dependencies {
        include(dependency('org.apache.commons:commons-math3:3.6.1'))
        include(dependency(name: 'packetevents-spigot-2.9.4'))
    }
}

build.dependsOn shadowJar
